/**
 * Copyright 2019 AppScale Systems, Inc
 *
 * SPDX-License-Identifier: Apache-2.0
 */

plugins {
  id "de.undercouch.download" version "3.4.3"
  id 'idea'
  id 'java'
}

ext {
  jettyVersion = '9.4.14.v20181114'
  sdkVersion = '1.9.73'
  sdkChecksum = '2c855a7f2a97dd43287569648b9175c4ab6dce61f1720ef02dc9308a74b33f00'
}

subprojects {
  apply plugin: 'idea'
  apply plugin: 'java-library'

  repositories {
    jcenter()
  }

  group = 'com.appscale.appengine'

  targetCompatibility = '1.8'
  sourceCompatibility = '1.8'
}

task downloadSdk(type: Download) {
  src "https://storage.googleapis.com/appengine-sdks/featured/appengine-java-sdk-${sdkVersion}.zip"
  dest file("$buildDir/sdk/appengine-java-sdk-${sdkVersion}.zip")
  overwrite false
}

task verifySdk(type: Verify, dependsOn: downloadSdk) {
  src file("$buildDir/sdk/appengine-java-sdk-${sdkVersion}.zip")
  algorithm 'SHA-256'
  checksum sdkChecksum
}

task unzipSdk(type: Copy, dependsOn: verifySdk) {
  from zipTree("$buildDir/sdk/appengine-java-sdk-${sdkVersion}.zip")
  into "$buildDir/sdk/orig"
}

task runtime(type: Zip, dependsOn: [assemble, unzipSdk]) {
  archiveName "appscale-java8-runtime-${sdkVersion}.zip"
  includeEmptyDirs true
  into('appscale-java8-runtime') {
    from zipTree("$buildDir/distributions/appscale-java8-runtime-root.zip")
    into('sdk') {
      from("$buildDir/sdk/orig/appengine-java-sdk-${sdkVersion}") {
        exclude "bin/**"
        exclude "demos/**"
        exclude "docs/**"
        exclude "jetty93-base/**"
        exclude "lib/java-managed-vm/**/*.jar"
        exclude "lib/opt/tools/**/*.jar"
        exclude "lib/opt/user/**/*.jar"
        exclude "lib/testing/**/*.jar"
        exclude "lib/tools/**/*.jar"
        exclude "lib/user/**/*.jar"
        exclude "src/**"
      }
      from zipTree("$buildDir/distributions/appscale-java8-runtime-sdk.zip")
    }
  }
}

task buildRootZip(type: Zip) {
  archiveName 'appscale-java8-runtime-root.zip'
  into('bin') {
    from fileTree('scripts')
  }
}

task buildSdkZip(type: Zip, dependsOn: [':appscale-java8-runtime-main:assemble', ':appscale-java8-runtime-container:assemble']) {
  archiveName 'appscale-java8-runtime-sdk.zip'
  into('lib') {
    from fileTree("${project(':appscale-java8-runtime-main').buildDir}/libs")
  }
  into('lib/shared') {
    from fileTree("${project(':appscale-java8-runtime-container').buildDir}/libs")
  }
}

assemble.dependsOn buildRootZip
assemble.dependsOn buildSdkZip
