/**
 * Copyright 2019 AppScale Systems, Inc
 *
 * SPDX-License-Identifier: Apache-2.0
 */

plugins {
  id "de.undercouch.download" version "3.4.3"
  id 'idea'
  id 'java'
}

ext {
  javaVersion = '1.8'
  jettyVersion = '9.4.14.v20181114'
  junitVersion = '5.4.2'
  sdkVersion = '1.9.75'
  sdkChecksum = '32490124d2c9d68f437997d39f07b18d894d23e0a90d2514ce623bd2964868e7'
}

subprojects {
  apply plugin: 'idea'
  apply plugin: 'java-library'

  repositories {
    jcenter()
  }

  group = 'com.appscale.appengine'

  targetCompatibility = javaVersion
  sourceCompatibility = javaVersion
}

idea {
  project {
    jdkName = javaVersion
    languageLevel = javaVersion
  }
}

jar {
  onlyIf { false }
}

task downloadSdk(type: Download) {
  src "https://storage.googleapis.com/appengine-sdks/featured/appengine-java-sdk-${sdkVersion}.zip"
  dest file("$buildDir/sdk/appengine-java-sdk-${sdkVersion}.zip")
  overwrite false
}

task verifySdk(type: Verify, dependsOn: downloadSdk) {
  src file("$buildDir/sdk/appengine-java-sdk-${sdkVersion}.zip")
  algorithm 'SHA-256'
  checksum sdkChecksum
}

task unzipSdk(type: Copy, dependsOn: verifySdk) {
  from zipTree("$buildDir/sdk/appengine-java-sdk-${sdkVersion}.zip")
  into "$buildDir/sdk/orig"
}

task runtime(type: Zip, dependsOn: [assemble, unzipSdk]) {
  archiveName "appscale-java8-runtime-${sdkVersion}.zip"
  includeEmptyDirs true
  into('appscale_java8_runtime') {
    from zipTree("$buildDir/distributions/appscale-java8-runtime-nosdk.zip")
    into('sdk') {
      from("$buildDir/sdk/orig/appengine-java-sdk-${sdkVersion}") {
        exclude "bin/**"
        exclude "demos/**"
        exclude "docs/**"
        exclude "jetty93-base/**"
        exclude "lib/java-managed-vm/**/*.jar"
        exclude "lib/opt/tools/**/*.jar"
        exclude "lib/opt/user/**/*.jar"
        exclude "lib/testing/**/*.jar"
        exclude "lib/tools/**/*.jar"
        exclude "lib/user/**/*.jar"
        exclude "src/**"
      }
    }
  }
}

task buildDistZip(type: Zip, dependsOn: [':appscale-java8-runtime-main:assemble', ':appscale-java8-runtime-container:assemble']) {
  archiveName 'appscale-java8-runtime-nosdk.zip'
  into('bin') {
    from fileTree('scripts')
  }
  into('lib') {
    from fileTree("${project(':appscale-java8-runtime-main').buildDir}/libs")
  }
  into('lib/shared') {
    from fileTree("${project(':appscale-java8-runtime-container').buildDir}/libs")
  }
}

assemble.dependsOn buildDistZip
